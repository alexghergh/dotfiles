# emacs style keybindings
bindkey -e

# unbind the arrow keys and the backspace key
bindkey -s "^[[A" ""
bindkey -s "^[[B" ""
bindkey -s "^[[C" ""
bindkey -s "^[[D" ""
bindkey -s "\x7f" ""

# prefix searching
#
# search for identic history up from the beginning of the line up to the cursor
# position
# !! `^x^n' overwrites `infer-next-history' (for now not needed, maybe in the
# future, for what it does, see https://zsh.sourceforge.io/Guide/zshguide04.html,
# search for `infer-next-history')
bindkey '^x^p' history-beginning-search-backward    # ctrl + x, ctrl + p
bindkey '^x^n' history-beginning-search-forward     # ctrl + x, ctrl + n

# copy the last shell argument
# useful for cases when you want to `mv' something like a large directory
# you only have to write the path once, then copy it and then simply change
# the last word
copy-prev-big-word() {
    local WORDCHARS="*?_-.[]~=/&;!#$%^(){}<>"
    zle copy-prev-word
}
zle -N copy-prev-big-word
bindkey '^[e' copy-prev-big-word    # Alt + e

# transpose 2 arguments in command line using Alt+z; the normal transpose-words
# using Alt+t still works; this has the added benefit the cursor doesn't move,
# though sometimes it might be an inconvenience
word-style-transpose() {
    emulate -L zsh
    local cursor_pos
    cursor_pos=$CURSOR

    local WORDCHARS="*?_-.[]~=/&;!#$%^(){}<>"
    zle transpose-words

    CURSOR=$cursor_pos
}
zle -N word-style-transpose
bindkey '^[z' word-style-transpose  # Alt + z

# replace the default push-line by push-line-or-edit
# has the same effect as normal <esc>-q when in the PS1 prompt, but has
# a different effect in PS2; namely, it brings the multiline command on the
# prompt, so you can scroll to lines above (this wasn't possible
# otherwise)
bindkey '^[q' push-line-or-edit     # Alt + q

# only navigate the local history (local to a shell, see `sharehistory' in .zsh_options)
# (see https://superuser.com/questions/446594/separate-up-arrow-lookback-for-local-and-global-zsh-history)
up-line-or-local-history() {
    zle set-local-history 1
    zle up-line-or-history
    zle set-local-history 0
}
zle -N up-line-or-local-history
bindkey "^x^h" up-line-or-local-history     # ctrl + x, ctrl + h

# only navigate the local history (local to a shell, see `share_history' in .zsh_options)
# (see https://superuser.com/questions/446594/separate-up-arrow-lookback-for-local-and-global-zsh-history)
down-line-or-local-history() {
    zle set-local-history 1
    zle down-line-or-history
    zle set-local-history 0
}
zle -N down-line-or-local-history
bindkey "^x^g" down-line-or-local-history   # ctrl + x, ctrl + g

# navigate backwards in the menu with shift-tab
bindkey '^[[Z' reverse-menu-complete        # shift + tab

# cycle through the possible path displays
# options are SHORT, LONG
# (see .zsh_misc_functions/custom_path.zsh)
cycle-path-display() {
    local current_mode
    current_mode=${_CURRENT_PATH_MODE:-LONG}

    # check if the user supplied mode is a valid mode
    if [[ ! "SHORT LONG" =~ "$current_mode" ]]; then
        current_mode="LONG"
    fi

    if [[ "$current_mode" == "SHORT" ]]; then
        _CURRENT_PATH_MODE="LONG"
    elif [[ "$current_mode" == "LONG" ]]; then
        _CURRENT_PATH_MODE="SHORT"
    fi
    zle reset-prompt
}
zle -N cycle-path-display
bindkey "^x^r" cycle-path-display
